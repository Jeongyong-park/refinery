plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id "java-library"
    id "maven-publish"
    id 'jacoco'
    id "com.diffplug.spotless" version "6.0.2"
}

group 'com.vortexa.refinery'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("11"))
    }

    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.poi:poi:5.2.2'
    api 'org.apache.poi:poi-ooxml:5.2.2'

    // testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

publishing {

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Refinery'
                url = 'https://github.com/VorTECHsa/refinery'
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/VorTECHsa/refinery"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

spotless {
    kotlin {
        ktlint("0.40.0")
    }
}
